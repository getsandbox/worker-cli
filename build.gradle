apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8
group = 'com.sandbox'
version = System.getProperty('sandbox_version') == null ? "1.0" : System.getProperty('sandbox_version')

repositories {
    mavenLocal()
    mavenCentral()
}

buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}

apply plugin: 'com.jfrog.bintray'

dependencies {
    compile project(':model')
    compile project(':jliquid')

    compile 'javax.servlet:javax.servlet-api:3.0.1'
    compile 'commons-io:commons-io:2.4'

    compile ('org.springframework:spring-core:4.1.8.RELEASE'){
        exclude group: 'commons-logging', module:'commons-logging'
    }
    compile 'org.springframework:spring-context-support:4.1.8.RELEASE'
    compile 'org.springframework:spring-expression:4.1.8.RELEASE'
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'org.slf4j:jcl-over-slf4j:1.7.7'
    compile 'ch.qos.logback:logback-classic:1.1.2'
    compile 'com.fasterxml.jackson.core:jackson-core:2.6.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.3'
    compile 'io.swagger:swagger-annotations:1.5.8'

    //to support parsing uri with params
    compile 'org.apache.cxf:cxf-rt-frontend-jaxrs:3.1.4'

    //inbound http requests
    compile 'org.eclipse.jetty:jetty-server:9.2.11.v20150529'

    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.8.4'
    testCompile 'org.springframework:spring-test:4.1.8.RELEASE'

}

jar {
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'

    manifest {
        attributes 'Main-Class': 'com.sandbox.runtime.config.CLIBootstrap'
    }

}

artifacts {
    archives jar
    archives shadowJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publish = true

    pkg {
        repo = 'public'
        name = 'runtime'
        desc = "Sandbox Runtime"
        userOrg = 'getsandbox'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/getsandbox/sandbox.git'

        version {
            name = System.getProperty('sandbox_version')
            desc = 'Sandbox Runtime'
            released = new Date()

            gpg {
                sign = true
            }

        }
    }

}

bintrayUpload.dependsOn(shadowJar)
